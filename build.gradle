buildscript {
    repositories {
        maven {
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: "com.github.johnrengelman.shadow"

version = '0.0.0'
group = 'top.seraphjack.healthcheck'
archivesBaseName = 'HealthCheck-1.18.1'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.18.1'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shadow
    compileOnly.extendsFrom(shadow)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.9'
    shadow 'org.apache.httpcomponents:httpclient:4.5.+'
    shadow 'commons-logging:commons-logging:1.1.+'
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'org.apache', 'top.seraphjack.healthcheck.repack.org.apache'
}

reobf {
    shadowJar {}
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
